use itertools::Itertools;
use core::ops::Range;
use crate::ast::ast_node::{AstNode, SpannedNode};
use crate::ast::assignment_node::{AssignmentOperation, AssignmentNode};
use crate::ast::array_node::ArrayNode;
use crate::ast::binary_op_node::{BinaryOpNode, BinaryOperation};
use crate::ast::block_node::BlockNode;
use crate::ast::call_node::CallNode;
use crate::ast::comma_expression_node::CommaExpressionNode;
use crate::ast::decl_node::DeclNode;
use crate::ast::do_while_node::DoWhileNode;
use crate::ast::expression_node::{ExpressionNode, first_span};
use crate::ast::float_node::FloatNode;
use crate::ast::for_node::ForNode;
use crate::ast::function_def_node::FunctionDefNode;
use crate::ast::if_node::IfNode;
use crate::ast::int_node::IntNode;
use crate::ast::mapping_node::MappingNode;
use crate::ast::program_node::ProgramNode;
use crate::ast::range_node::RangeNode;
use crate::ast::return_node::ReturnNode;
use crate::ast::string_node::StringNode;
use crate::ast::unary_op_node::{UnaryOpNode, UnaryOperation};
use crate::ast::var_node::VarNode;
use crate::ast::var_init_node::VarInitNode;
use crate::ast::while_node::WhileNode;
use crate::semantic::lpc_type::LpcType;
use crate::parser::collapse_binary_op::collapse_binary_op;
use crate::parser::collapse_unary_op::collapse_unary_op;
use crate::parser::span::Span;
use crate::parser::lexer::Token;
use crate::errors::LpcError;
use crate::parser::lexer::logos_token::{StringToken, IntToken, FloatToken};
use crate::LpcFloat;

grammar;

extern {
    type Location = usize;
    type Error = LpcError;

    enum Token {
        "+" => Token::Plus(<Span>),
        "-" => Token::Minus(<Span>),
        "*" => Token::Mul(<Span>),
        "/" => Token::Div(<Span>),
        "%" => Token::Mod(<Span>),
        "!" => Token::Bang(<Span>),
        "^" => Token::Caret(<Span>),
        "~" => Token::Tilde(<Span>),
        "&" => Token::And(<Span>),
        "&&" => Token::AndAnd(<Span>),
        "|" => Token::Or(<Span>),
        "||" => Token::OrOr(<Span>),
        "<<" => Token::LeftShift(<Span>),
        ">>" => Token::RightShift(<Span>),
        "==" => Token::EqEq(<Span>),
        "!=" => Token::NotEq(<Span>),
        "<" => Token::LessThan(<Span>),
        "<=" => Token::LessThanEq(<Span>),
        ">" => Token::GreaterThan(<Span>),
        ">=" => Token::GreaterThanEq(<Span>),

        "=" => Token::Assign(<Span>),
        "+=" => Token::PlusEq(<Span>),
        "-=" => Token::MinusEq(<Span>),
        "*=" => Token::MulEq(<Span>),
        "/=" => Token::DivEq(<Span>),
        "%=" => Token::ModEq(<Span>),
        "^=" => Token::CaretEq(<Span>),
        "~=" => Token::TildeEq(<Span>),
        "&=" => Token::AndEq(<Span>),
        "&&=" => Token::AndAndEq(<Span>),
        "|=" => Token::OrEq(<Span>),
        "||=" => Token::OrOrEq(<Span>),
        "<<=" => Token::LeftShiftEq(<Span>),
        ">>=" => Token::RightShiftEq(<Span>),

        "if" => Token::If(<Span>),
        "else" => Token::Else(<Span>),
        "while" => Token::While(<Span>),
        "for" => Token::For(<Span>),
        "inherit" => Token::Inherit(<Span>),
        "break" => Token::Break(<Span>),
        "continue" => Token::Continue(<Span>),
        "case" => Token::Case(<Span>),
        "do" => Token::Do(<Span>),
        "int" => Token::Int(<Span>),
        "float" => Token::Float(<Span>),
        "string" => Token::String(<Span>),
        "object" => Token::Object(<Span>),
        "mapping" => Token::Mapping(<Span>),
        "mixed" => Token::Mixed(<Span>),
        "void" => Token::Void(<Span>),
        "return" => Token::Return(<Span>),
        "static" => Token::Static(<Span>),
        "nomask" => Token::Nomask(<Span>),
        "efun" => Token::Efun(<Span>),

        "(" => Token::LParen(<Span>),
        ")" => Token::RParen(<Span>),
        "[" => Token::LBracket(<Span>),
        "]" => Token::RBracket(<Span>),
        "{" => Token::LBrace(<Span>),
        "}" => Token::RBrace(<Span>),
        "," => Token::Comma(<Span>),
        "->" => Token::CallOther(<Span>),
        "?" => Token::Question(<Span>),
        ":" => Token::Colon(<Span>),
        "::" => Token::ColonColon(<Span>),
        ";" => Token::Semi(<Span>),
        "..." => Token::Ellipsis(<Span>),
        ".." => Token::Range(<Span>),

        "StringLiteral" => Token::StringLiteral(<StringToken>),
        "IntLiteral" => Token::IntLiteral(<IntToken>),
        "FloatLiteral" => Token::FloatLiteral(<FloatToken>),
        "ID" => Token::Id(<StringToken>),
    }
}

pub Program: ProgramNode = <body:Def*> => ProgramNode {
    body: body.iter().map(|i| AstNode::from(i.clone())).collect::<Vec<_>>()
};

pub Def: AstNode = {
    <FunctionDef> => AstNode::from(<>),
    <decl:Decl> ";" => {
        let mut node = decl.clone();
        node.initializations.iter_mut().for_each(|i| i.global = true);

        AstNode::from(node)
    }
};

FunctionDef: FunctionDefNode = {
    <r_type:Type?> <star:"*"?> <name:"ID"> <parameters:Parameters> "{" <body:Statements> "}" => {
        let array = star.is_some();

        let l = if let Some((_, span)) = &r_type {
           span.l
        } else if let Some(span) = &star {
           span.l
        } else {
           name.0.l
        };

        let r = name.0.r;

         let return_type = if let Some(this_type) = r_type {
             match this_type.0.as_str() {
                 "void" => LpcType::Void,
                 "int" => LpcType::Int(array),
                 "string" => LpcType::String(array),
                 "float" => LpcType::Float(array),
                 // "object" => LpcType::Object(array),
                 "mapping" => LpcType::Mapping(array),
                 "mixed" => LpcType::Mixed(array),
                 _ => unimplemented!()
             }
         } else {
             LpcType::Int(array)
         };

        let file_id = name.0.file_id;
        FunctionDefNode {
            name: name.1,
            parameters,
            body,
            return_type,
            span: Some(Span::new(file_id, l..r))
        }
    },
}

Parameters: Vec<VarInitNode> = {
    "(" <parm:Parameter?> ")" => {
        parm.unwrap_or(vec![])
    }
};

// TODO: ellipsis
Parameter: Vec<VarInitNode> = {
    <parms:ParameterList> <_ellipsis:("," "...")?> => {
        parms
    }
};

ParameterList: Vec<VarInitNode> = {
    <list:(<ParameterList> ",")?> <name:ArgName> => {
        let mut v = list.unwrap_or(vec![]);
        v.push(name);
        v
    }
};

ArgName: VarInitNode = {
    <var_type:Type> <mut init:VarInit> => {
        let new_type = LpcType::from(var_type.0);

        init.type_ = new_type.as_array(init.array);

        init
    }
};

Type: (String, Span) = {
    "int" => (String::from("int"), <>),
    "float" => (String::from("float"), <>),
    "string" => (String::from("string"), <>),
    "mixed" => (String::from("mixed"), <>),
    "mapping" => (String::from("mapping"), <>),
    "void" => (String::from("void"), <>),
};

pub Statement: AstNode = {
    <OpenStatement>,
    <ClosedStatement>,
};

pub Statements: Vec<AstNode> = {
    <Statement*>
};

pub ExpressionStatement: Option<ExpressionNode> = {
    <Expression?> ";"
}

pub SimpleStatement: AstNode = {
    <Expression> ";" => AstNode::from(<>),
    <Return> ";" => AstNode::from(<>),
    <Decl> ";" => AstNode::from(<>),
    <Block> => AstNode::from(<>),
    <DoWhile> => AstNode::from(<>)
};

OpenStatement: AstNode = {
    <if_t:"if"> "(" <cond:Expression> ")" <body:Statement> => {
        // https://github.com/lalrpop/lalrpop/issues/67#issuecomment-188951041
        let node = IfNode::new(cond, body, None, Some(if_t));
        AstNode::from(node)
    },
    <if_t:"if"> "(" <cond:Expression> ")" <body:ClosedStatement> "else" <else_clause:OpenStatement> => {
        let node = IfNode::new(cond, body, Some(else_clause), Some(if_t));
        AstNode::from(node)
    },
    <while_t:"while"> "(" <cond:Expression> ")" <body:OpenStatement> => {
        let node = WhileNode::new(cond, body, Some(while_t));
        AstNode::from(node)
    },
    <for_t:"for"> "(" <init:ForInit?> ";" <cond:Expression?> ";" <inc:Expression?> ")" <body:OpenStatement> => {
        let node = ForNode::new(init, cond, inc, body, Some(for_t));
        AstNode::from(node)
    }
};

ClosedStatement: AstNode = {
    <SimpleStatement>,
    <if_t:"if"> "(" <cond:Expression> ")" < body:ClosedStatement> "else" <else_clause:ClosedStatement> => {
        let node = IfNode::new(cond, body, Some(else_clause), Some(if_t));
        AstNode::from(node)
    },
     <while_t:"while"> "(" <cond:Expression> ")" <body:ClosedStatement> => {
        let node = WhileNode::new(cond, body, Some(while_t));
        AstNode::from(node)
     },
    <for_t:"for"> "(" <init:ForInit?> ";" <cond:Expression?> ";" <inc:Expression?> ")" <body:ClosedStatement> => {
        let node = ForNode::new(init, cond, inc, body, Some(for_t));
        AstNode::from(node)
    }
};

ForInit: AstNode = {
    <Decl> => AstNode::from(<>),
    <Expression> => AstNode::from(<>)
};

DoWhile: DoWhileNode = {
    <do_t:"do"> <body:Statement> "while" "(" <cond:Expression> ")" ";" => {
        DoWhileNode::new(body, cond, Some(do_t))
    }
};

VarInit: VarInitNode = {
    <array:"*"?> <name:"ID"> <value:("=" <AssignmentExpression>)?> => {
        let name_span = name.0;
        let file_id = name_span.file_id;
        let l = array.unwrap_or(name_span).l;

        let r = if let Some(node) = &value {
            node.span().unwrap_or(name_span).r
        } else {
            name_span.r
        };

        VarInitNode {
            type_: LpcType::Int(false), // placeholder. Corrected in Decl.
            name: name.1,
            value,
            array: array.is_some(),
            global: false,
            span: Some(Span::new(file_id, l..r))
        }
    }
}

pub Decl: DeclNode = {
    <var_type:Type> <init:VarInit> <optional:("," <VarInit>)*> => {
        let new_type = LpcType::from(var_type.0).as_array(init.array);
        let mut initializations = vec![init];

        initializations.append(optional.to_vec().as_mut());

        // Update each VarInitNode with the correct type.
        for init in &mut initializations {
            init.update_type(new_type)
        }

        DeclNode {
            type_: new_type,
            initializations
        }
    }
}

pub Block: BlockNode = {
    "{" <Statements> "}" => {
        BlockNode::new(<>)
    }
};

pub PrimaryExpression: ExpressionNode = {
    <Int>,
    <Float>,
    <String>,
    <Var>,
    "(" <Expression> ")",
    "(" "{" <l:ExpressionList?> "}" ")" => {
        let vec = l.unwrap_or(vec![]);

        ExpressionNode::Array(ArrayNode::new(vec))
    },
    <l:"("> "[" <list:MappingExpressionList?> "]" <r:")"> => {
         let vec = if let Some(v) = list {
             v.into_iter().rev().unique_by(|(k, _)| k.to_string()).collect_vec()
         } else {
             Vec::new()
         };

         let span = Span {
            r: r.r,
            ..l
         };

         ExpressionNode::Mapping(MappingNode::new(vec, Some(span)))
    }
};

pub PostfixExpression: ExpressionNode = {
    <PrimaryExpression>,
    <l:PostfixExpression> "[" <r:Expression> <right:"]"> => {
        let file_id = first_span(&[&l, &r]).file_id;
        let left = if let Some(span) = l.span() {
            span.l
        } else {
            0
        };

        collapse_binary_op(BinaryOperation::Index, l, r, Span { file_id, l: left, r: right.r })
    },
    <l:PostfixExpression> "["  <e1:Expression> ".." <e2:Expression> <right:"]"> => {
        let file_id = first_span(&[&l, &e1, &e2]).file_id;
        let left = if let Some(span) = l.span() {
            span.l
        } else {
            0
        };

        let r = ExpressionNode::Range(RangeNode {
            l: Box::new(Some(e1)),
            r: Box::new(Some(e2)),
            span: Some(Span { file_id, l: left, r: right.r })
        });

        collapse_binary_op(BinaryOperation::Index, l, r, Span { file_id, l: left, r: right.r })
    },
    <l:PostfixExpression> "[" ".." <e:Expression> <right:"]"> => {
        let file_id = first_span(&[&l, &e]).file_id;
        let left = if let Some(span) = l.span() {
            span.l
        } else {
            0
        };

        let r = ExpressionNode::Range(RangeNode {
            l: Box::new(None),
            r: Box::new(Some(e)),
            span: Some(Span { file_id, l: left, r: right.r })
        });

        collapse_binary_op(BinaryOperation::Index, l, r, Span { file_id, l: left, r: right.r })
    },
    <l:PostfixExpression> "[" <e:Expression> ".." <right:"]"> => {
        let file_id = first_span(&[&l, &e]).file_id;
        let left = if let Some(span) = l.span() {
            span.l
        } else {
            0
        };

        let r = ExpressionNode::Range(RangeNode {
            l: Box::new(Some(e)),
            r: Box::new(None),
            span: Some(Span { file_id, l: left, r: right.r })
        });

        collapse_binary_op(BinaryOperation::Index, l, r, Span { file_id, l: left, r: right.r })
    },
    Call => ExpressionNode::Call(<>)
};

pub UnaryExpression: ExpressionNode = {
    <PostfixExpression>,
    <op:UnaryOp> <expr:CastExpression> => {
        let span = if let Some(s) = expr.span() {
            Some(Span {
                l: op.1.start,
                ..s
            })
        } else {
            None
        };

        let n = UnaryOpNode {
            expr: Box::new(expr),
            op: op.0,
            is_post: false,
            span
        };

        collapse_unary_op(n)
    }
};

pub CastExpression: ExpressionNode = {
    <UnaryExpression>,
}

pub MultiplicitiveExpression: ExpressionNode = {
    <CastExpression>,
    <l:MultiplicitiveExpression> <op:MultiplicitiveBinaryOp> <r:CastExpression> => {
         let file_id = first_span(&[&l, &r]).file_id;
         let left = if let Some(span) = l.span() {
             span.l
         } else {
             0
         };
         let right = if let Some(span) = r.span() {
             span.r
         } else {
             0
         };

         collapse_binary_op(op, l, r, Span { file_id, l: left, r: right })
    },
};

pub AdditiveExpression: ExpressionNode = {
    <MultiplicitiveExpression>,
    <l:AdditiveExpression> <op:AdditiveBinaryOp> <r:MultiplicitiveExpression> => {
        let file_id = first_span(&[&l, &r]).file_id;
         let left = if let Some(span) = l.span() {
             span.l
         } else {
             0
         };
         let right = if let Some(span) = r.span() {
             span.r
         } else {
             0
         };

        collapse_binary_op(op, l, r, Span { file_id, l: left, r: right })
    }
};

pub ShiftExpression: ExpressionNode = {
    <AdditiveExpression>,
};

pub RelationalExpression: ExpressionNode = {
    <ShiftExpression>,
    <l:RelationalExpression> <op:RelationalOp> <r:ShiftExpression> => {
         let file_id = first_span(&[&l, &r]).file_id;
         let left = if let Some(span) = l.span() {
             span.l
         } else {
             0
         };
         let right = if let Some(span) = r.span() {
             span.r
         } else {
             0
         };

         collapse_binary_op(op, l, r, Span { file_id, l: left, r: right })
    }
};

pub EqualityExpression: ExpressionNode = {
    <RelationalExpression>,
    <l:EqualityExpression> <op:EqualityOp> <r:RelationalExpression> => {
         let file_id = first_span(&[&l, &r]).file_id;
         let left = if let Some(span) = l.span() {
             span.l
         } else {
             0
         };
         let right = if let Some(span) = r.span() {
             span.r
         } else {
             0
         };

         collapse_binary_op(op, l, r, Span { file_id, l: left, r: right })
    }
};

pub AndExpression: ExpressionNode = {
    <EqualityExpression>
};

pub ExclusiveOrExpression: ExpressionNode = {
    <AndExpression>
};

pub OrExpression: ExpressionNode = {
    <ExclusiveOrExpression>
};

pub LogicalAndExpression: ExpressionNode = {
    <OrExpression>
};

pub LogicalOrExpression: ExpressionNode = {
    <LogicalAndExpression>
};

pub ConditionalExpression: ExpressionNode = {
    <LogicalOrExpression>
};

AssignmentExpression: ExpressionNode = {
    <ConditionalExpression>,
    <lhs:UnaryExpression> <op:AssignmentOp> <rhs:AssignmentExpression> => {
        let file_id = first_span(&[&lhs, &rhs]).file_id;
         let l = if let Some(span) = lhs.span() {
             span.l
         } else {
             0
         };
         let r = if let Some(span) = rhs.span() {
             span.r
         } else {
             0
         };

        // Decompose `op=` operators (e.g. `x += y`, etc.) into `x = x op y`
        let rhs = match op {
            AssignmentOperation::Simple => {
                rhs
            }
            AssignmentOperation::PlusEq
            | AssignmentOperation::MinusEq
            | AssignmentOperation::MulEq
            | AssignmentOperation::DivEq => {
                let binary_op = match op {
                    AssignmentOperation::PlusEq => BinaryOperation::Add,
                    AssignmentOperation::MinusEq => BinaryOperation::Sub,
                    AssignmentOperation::MulEq => BinaryOperation::Mul,
                    AssignmentOperation::DivEq => BinaryOperation::Div,
                    _ => panic!("This only way for this to be reached is if a new op was added in the parent `match`, \
                                without updating this one.")
                };

                ExpressionNode::BinaryOp(BinaryOpNode {
                    op: binary_op,
                    span: rhs.span(),
                    l: Box::new(lhs.clone()),
                    r: Box::new(rhs),
                })

            }
            _ => panic!("unknown operation in assignment: `{}`", op)
        };

        ExpressionNode::Assignment(AssignmentNode {
            lhs: Box::new(lhs),
            rhs: Box::new(rhs),
            op: AssignmentOperation::Simple,
            span: Some(Span { file_id, l, r }),
        })
    }
};

pub Expression: ExpressionNode = {
    <AssignmentExpression>,
    <e:Expression> <c:","> <ass:AssignmentExpression> => {
        let file_id = if let Some(s) = e.span() {
            s.file_id
        } else if let Some(s) = ass.span() {
            s.file_id
        } else {
            c.file_id
        };

        let l = e.span().unwrap_or(c).l;
        let r = ass.span().unwrap_or(c).r;

        let mut list = if let ExpressionNode::CommaExpression(ce) = e {
            ce.value
        } else {
            vec![e]
        };

        list.extend_from_slice(&[ass]);

        ExpressionNode::CommaExpression(CommaExpressionNode {
            value: list,
            span: Some(Span { file_id, l, r })
        })
    }
};

pub Return: ReturnNode = {
    <ret:"return"> <e:Expression?> => {
        let r = if let Some(expr) = &e {
            expr.span().unwrap_or(ret).r
        } else {
            ret.r
        };

        ReturnNode { value: e, span: Some(Span { file_id: ret.file_id, l: ret.l, r }) }
    }
}

ExpressionList: Vec<ExpressionNode> = {
    <AssignmentExpression> => vec![<>],
    <list:ExpressionList> "," <arg:AssignmentExpression> => {
        let mut v = list;
        v.push(arg);
        v
    }
};

ArgumentExpressionList: Vec<ExpressionNode> = {
    <AssignmentExpression> => vec![<>],
    <list:ArgumentExpressionList> "," <arg:AssignmentExpression> => {
        let mut v = list;
        v.push(arg);
        v
    }
};

MappingExpressionList: Vec<(ExpressionNode, ExpressionNode)> = {
    <MappingInitializer> => {
        vec![(<>.0, <>.1)]
    },
    <vec:MappingExpressionList> "," <arg:MappingInitializer> => {
        let mut v = vec;
        v.push((arg.0, arg.1));
        v
    }
};

MappingInitializer: (ExpressionNode, ExpressionNode) = {
    <a:AssignmentExpression> ":" <b:AssignmentExpression> => {
        (a, b)
    }
}

pub Call: CallNode = {
    <name:"ID"> "(" <list:ArgumentExpressionList?> <right:")"> => {
        let arguments = match list {
            Some(args) => args,
            None => vec![]
        };

        let file_id = name.0.file_id;
        let l = name.0.l;
        let r = right.r;

        CallNode {
            arguments,
            name: name.1,
            span: Some(Span { file_id, l, r })
        }
    }
};

AssignmentOp: AssignmentOperation = {
    "=" => AssignmentOperation::Simple,
    "+=" => AssignmentOperation::PlusEq,
    "-=" => AssignmentOperation::MinusEq,
    "*=" => AssignmentOperation::MulEq,
    "/=" => AssignmentOperation::DivEq,
};

AdditiveBinaryOp: BinaryOperation = {
    "+" => BinaryOperation::Add,
    "-" => BinaryOperation::Sub
};

MultiplicitiveBinaryOp: BinaryOperation = {
    "*" => BinaryOperation::Mul,
    "/" => BinaryOperation::Div
};

EqualityOp: BinaryOperation = {
    "==" => BinaryOperation::EqEq,
};

RelationalOp: BinaryOperation = {
    "<" => BinaryOperation::Lt,
    "<=" => BinaryOperation::Lte,
    ">" => BinaryOperation::Gt,
    ">=" => BinaryOperation::Gte,
};

UnaryOp: (UnaryOperation, Range<usize>) = {
    <l:@L> "-" <r:@R> => (UnaryOperation::Negate, l..r),
//    "++" => UnaryOperation::Inc,
//    "--" => UnaryOperation::Dec,
//    "!" => UnaryOperation::Bang,
//    "~" => UnaryOperation::Tilde,
}

Var: ExpressionNode = <name:"ID"> => {
    ExpressionNode::Var(
        VarNode {
            name: name.1,
            span: Some(name.0),
            global: false
        }
    )
};

Int: ExpressionNode = {
    <i:"IntLiteral"> => {
        ExpressionNode::Int(
            IntNode {
                value: i.1,
                span: Some(i.0),
            }
        )
    },
};

Float: ExpressionNode = <f:"FloatLiteral"> => {
    ExpressionNode::Float(
        FloatNode {
            value: LpcFloat::from(f.1),
            span: Some(f.0)
        }
    )
};

String: ExpressionNode = {
    <s:"StringLiteral"> => {
        ExpressionNode::String(
            StringNode {
                value: s.1,
                span: Some(s.0)
            }
        )
    },
    <s1:"StringLiteral"> <s2:String> => {
        let span = s1.0;
        let file_id = span.file_id;
        let l = span.l;

        match s2 {
            ExpressionNode::String(node) => {
                let r = if let Some(span) = &node.span {
                    span.r
                } else {
                    l
                };
                ExpressionNode::String(StringNode {
                    value: s1.1 + &node.value,
                    span: Some(Span { file_id, l, r })
                })
            }
            _ => unimplemented!()
        }
    },
};
