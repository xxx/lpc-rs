use std::str::FromStr;

use crate::ast::program_node::ProgramNode;
use crate::ast::binary_op_node::BinaryOperation;
use crate::ast::expression_node::ExpressionNode;
use crate::ast::function_def_node::FunctionDefNode;
use crate::ast::int_node::IntNode;
use crate::ast::float_node::FloatNode;
use crate::ast::call_node::CallNode;
use crate::ast::return_node::ReturnNode;
use crate::ast::decl_node::DeclNode;
use crate::ast::var_init_node::VarInitNode;
use crate::ast::range_node::RangeNode;
use crate::ast::var_node::VarNode;
use crate::ast::string_node::StringNode;
use crate::ast::assignment_node::{AssignmentOperation, AssignmentNode};
use crate::ast::array_node::ArrayNode;
use crate::ast::comma_expression_node::CommaExpressionNode;
use crate::semantic::lpc_type::LPCType;
use crate::ast::ast_node::ASTNode;
use crate::parser::collapse_expression::collapse_expression;
use crate::parser::span::Span;

grammar;

match {
    r"\s*" => { }, // The default whitespace skipping is disabled if an `ignore pattern` is specified
    r"//[^\n\r]*[\n\r]*" => { }, // Skip `// comments`
    r"/\*([^*]*\*+[^*/])*([^*]*\*+|[^*])*\*/" => { },  // Skip `/* comments */`
    _ // let everything else through
}

SemiSeq<T>: Vec<T> = {
    <v:(<T> ";")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

pub Program: ProgramNode = <body:Def*> => ProgramNode {
    body: body.iter().map(|i| ASTNode::from(i.clone())).collect::<Vec<_>>()
};

pub Def: ASTNode = {
    <FunctionDef> => ASTNode::from(<>),
    <decl:Decl> ";" => {
        let mut node = decl.clone();
        node.initializations.iter_mut().for_each(|i| i.global = true);

        ASTNode::from(node)
    }
};

FunctionDef: FunctionDefNode = {
    <l:@L> <r_type:Type?> <star:"*"?> <name:ID> <parameters:Parameters> <r:@R> "{" <body:SemiSeq<Statement>> "}" => {
         let array = star.is_some();

         let return_type = if let Some(this_type) = r_type {
             match this_type.as_str() {
                 "void" => LPCType::Void,
                 "int" => LPCType::Int(array),
                 "string" => LPCType::String(array),
                 "float" => LPCType::Float(array),
                 // "object" => LPCType::Object(array),
                 // "mapping" => LPCType::Mapping(array),
                 "mixed" => LPCType::Mixed(array),
                 _ => unimplemented!()
             }
         } else {
             LPCType::Int(array)
         };

        FunctionDefNode {
            name,
            parameters,
            body,
            return_type,
            span: Some(Span { l, r })
        }
    },
}

Parameters: Vec<VarInitNode> = {
    "(" <parm:Parameter?> ")" => {
        parm.unwrap_or(vec![])
    }
};

// TODO: ellipsis
Parameter: Vec<VarInitNode> = {
    <parms:ParameterList> <ellipsis:("," "...")?> => {
        parms
    }
};

ParameterList: Vec<VarInitNode> = {
    <list:(<ParameterList> ",")?> <name:ArgName> => {
        let mut v = list.unwrap_or(vec![]);
        v.push(name);
        v
    }
};

ArgName: VarInitNode = {
    <var_type:Type> <mut init:VarInit> => {
        let new_type = LPCType::from(var_type);

        init.type_ = new_type.as_array(init.array);

        init
    }
};

Type: String = {
    "int" => String::from(<>),
    "float" => String::from(<>),
    "string" => String::from(<>),
    "mixed" => String::from(<>),
    "void" => String::from(<>),
};

pub Statement: ASTNode = {
    <Expression> => ASTNode::from(<>),
    <Return> => ASTNode::from(<>),
    <Decl> => ASTNode::from(<>)
};

VarInit: VarInitNode = {
    <l:@L> <array:"*"?> <name:ID> <value:("=" <AssignmentExpression>)?> <r:@R> => {
        VarInitNode {
            type_: LPCType::Int(false), // placeholder. Corrected in Decl.
            name,
            value,
            array: array.is_some(),
            global: false,
            span: Some(Span { l, r })
        }
    }
}

pub Decl: DeclNode = {
    <var_type:Type> <init:VarInit> <optional:("," <VarInit>)*> => {
        let new_type = LPCType::from(var_type).as_array(init.array);
        let mut initializations = vec![init];

        initializations.append(optional.to_vec().as_mut());

        // Update each VarInitNode with the correct type.
        for init in &mut initializations {
            init.update_type(new_type)
        }

        DeclNode {
            type_: new_type,
            initializations
        }
    }
}

pub PrimaryExpression: ExpressionNode = {
    <Int>,
    <Float>,
    <String>,
    <Var>,
    "(" <Expression> ")",
    "(" "{" <l:ExpressionList?> "}" ")" => {
        let vec = if let Some(list) = l {
            list
        } else {
            vec![]
        };

        ExpressionNode::Array(ArrayNode::new(vec))
    }
};

pub PostfixExpression: ExpressionNode = {
    <PrimaryExpression>,
    <left:@L> <l:PostfixExpression> "[" <r:Expression> "]" <right:@R> => {
        collapse_expression(BinaryOperation::Index, l, r, Span { l: left, r: right })
    },
    <left:@L> <l:PostfixExpression> "["  <e1:Expression> ".." <e2:Expression> "]" <right:@R> => {
        let r = ExpressionNode::Range(RangeNode {
            l: Box::new(Some(e1)),
            r: Box::new(Some(e2)),
            span: Some(Span { l: left, r: right })
        });

        collapse_expression(BinaryOperation::Index, l, r, Span { l: left, r: right })
    },
    <left:@L> <l:PostfixExpression> "[" ".." <e:Expression> "]" <right:@R> => {
        let r = ExpressionNode::Range(RangeNode {
            l: Box::new(None),
            r: Box::new(Some(e)),
            span: Some(Span { l: left, r: right })
        });

        collapse_expression(BinaryOperation::Index, l, r, Span { l: left, r: right })
    },
    <left:@L> <l:PostfixExpression> "[" <e:Expression> ".." "]" <right:@R> => {
        let r = ExpressionNode::Range(RangeNode {
            l: Box::new(Some(e)),
            r: Box::new(None),
            span: Some(Span { l: left, r: right })
        });

        collapse_expression(BinaryOperation::Index, l, r, Span { l: left, r: right })
    },
    Call => ExpressionNode::Call(<>)
};

pub UnaryExpression: ExpressionNode = {
    <PostfixExpression>,
};

pub CastExpression: ExpressionNode = {
    <UnaryExpression>,
}

pub MultiplicitiveExpression: ExpressionNode = {
    <CastExpression>,
    <left:@L> <l:MultiplicitiveExpression> <op:MultiplicitiveBinaryOp> <r:CastExpression> <right:@R> => {
         collapse_expression(op, l, r, Span { l: left, r: right })
    },
};

pub AdditiveExpression: ExpressionNode = {
    <MultiplicitiveExpression>,
    <left:@L> <l:AdditiveExpression> <op:AdditiveBinaryOp> <r:MultiplicitiveExpression> <right:@R> => {
        collapse_expression(op, l, r, Span { l: left, r: right })
    }
};

pub ShiftExpression: ExpressionNode = {
    <AdditiveExpression>,
};

pub RelationalExpression: ExpressionNode = {
    <ShiftExpression>
};

pub EqualityExpression: ExpressionNode = {
    <RelationalExpression>
};

pub AndExpression: ExpressionNode = {
    <EqualityExpression>
};

pub ExclusiveOrExpression: ExpressionNode = {
    <AndExpression>
};

pub OrExpression: ExpressionNode = {
    <ExclusiveOrExpression>
};

pub LogicalAndExpression: ExpressionNode = {
    <OrExpression>
};

pub LogicalOrExpression: ExpressionNode = {
    <LogicalAndExpression>
};

pub ConditionalExpression: ExpressionNode = {
    <LogicalOrExpression>
};

AssignmentExpression: ExpressionNode = {
    <ConditionalExpression>,
    <l:@L> <lhs:UnaryExpression> <op:AssignmentOp> <rhs:AssignmentExpression> <r:@R> => ExpressionNode::Assignment(AssignmentNode {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op,
        span: Some(Span { l, r })
    })
};

pub Expression: ExpressionNode = {
    <AssignmentExpression>,
    <l:@L> <e:Expression> "," <ass:AssignmentExpression> <r:@R> => {
        let mut list = if let ExpressionNode::CommaExpression(ce) = e {
            ce.value
        } else {
            vec![e]
        };

        list.extend_from_slice(&[ass]);

        ExpressionNode::CommaExpression(CommaExpressionNode {
            value: list,
            span: Some(Span { l, r })
        })
    }
};

pub Return: ReturnNode = {
    <l:@L> "return" <e:Expression?> <r:@R> => ReturnNode { value: e, span: Some(Span { l, r }) }
}

ExpressionList: Vec<ExpressionNode> = {
    <AssignmentExpression> => vec![<>],
    <list:ExpressionList> "," <arg:AssignmentExpression> => {
        let mut v = list;
        v.push(arg);
        v
    }
};

ArgumentExpressionList: Vec<ExpressionNode> = {
    <AssignmentExpression> => vec![<>],
    <list:ArgumentExpressionList> "," <arg:AssignmentExpression> => {
        let mut v = list;
        v.push(arg);
        v
    }
};

pub Call: CallNode = {
    <l:@L> <name:ID> "(" <list:ArgumentExpressionList?> ")" <r:@R> => {
        let arguments = match list {
            Some(args) => args,
            None => vec![]
        };

        CallNode {
            arguments,
            name,
            span: Some(Span { l, r })
        }
    }
};

AssignmentOp: AssignmentOperation = {
    "=" => AssignmentOperation::Simple
};

AdditiveBinaryOp: BinaryOperation = {
    "+" => BinaryOperation::Add,
    "-" => BinaryOperation::Sub
};

MultiplicitiveBinaryOp: BinaryOperation = {
    "*" => BinaryOperation::Mul,
    "/" => BinaryOperation::Div
};

Var: ExpressionNode = <l:@L> <name:ID> <r:@R> => ExpressionNode::Var(
    VarNode {
        name,
        span: Some(Span { l, r }),
        global: false
    }
);

Int: ExpressionNode = {
    <l:@L> <s:r"[1-9][0-9_]*"> <r:@R> => ExpressionNode::Int(
        IntNode {
            value: i64::from_str(&s.replace("_", "")).unwrap(),
            span: Some(Span { l, r })
        }
    ),
    <l:@L> <s:r"0[xX][0-9a-fA-F][0-9a-fA-F_]*"> <r:@R> => ExpressionNode::Int(
        IntNode {
            value: i64::from_str_radix(
                &s.replace("_", "")
                  .trim_start_matches("0x")
                  .trim_start_matches("0X"),
                16).unwrap(),
            span: Some(Span { l, r })
        }
    ),
    <l:@L> <s:r"0[0-7][0-7_]*"> <r:@R> => ExpressionNode::Int(
        IntNode {
            value: i64::from_str_radix(&s.replace("_", ""), 8).unwrap(),
            span: Some(Span { l, r })
        }
    ),
    <l:@L> <s:r"0[bB][01][01_]*"> <r:@R> => ExpressionNode::Int(
        IntNode {
            value: i64::from_str_radix(
                &s.replace("_", "")
                  .trim_start_matches("0b")
                  .trim_start_matches("0B"),
                2).unwrap(),
            span: Some(Span { l, r })
        }
    ),
};

Float: ExpressionNode = <l:@L> <s:r"[0-9][0-9_]*\.[0-9][0-9_]*(?:[eE][-+]?[0-9][0-9_]*)?"> <r:@R> => ExpressionNode::Float(
    FloatNode {
        value: f64::from_str(&s.replace("_", "")).unwrap(),
        span: Some(Span { l, r })
    }
);

String: ExpressionNode = {
    <l:@L> <s:StringLiteral> <r:@R> => {
        ExpressionNode::String(
            StringNode {
                value: s,
                span: Some(Span { l, r })
            }
        )
    },
    <l:@L> <s1:StringLiteral> <s2:String> <r:@R> => {
        match s2 {
            ExpressionNode::String(node) => {
                ExpressionNode::String(StringNode {
                    value: s1 + &node.value,
                    span: Some(Span { l, r })
                })
            }
            _ => unimplemented!()
        }
    },
};

StringLiteral: String = <s:r#""(\\.|[^"])*""#> => {
        // Strip the quotes at each end.
        let value = if s.len() < 3 { "" } else { &s[1..=(s.len() - 2)] };

        value.to_string()
            .replace("\\n", "\n")
            .replace("\\r", "\r")
            .replace("\\t", "\t")
            .replace("\\v", "\x0F")
            .replace("\\f", "\x0C")
            .replace("\\a", "\x07")
            .replace("\\b", "\x08")
};

ID: String = r"[\p{Alphabetic}_]\w*" => String::from(<>);